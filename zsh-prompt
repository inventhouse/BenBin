#!/bin/zsh

# TODO: more configurability (e.g. colors, max length of pwd, etc.)

###  Prompt Helpers  ###
branch_name() {
    # Check if we are in a git repository (avoiding errors outside repos)
    git rev-parse --is-inside-work-tree >/dev/null 2>&1 || return

    # Get the abbreviated branch name
    local branch=$(git rev-parse --abbrev-ref HEAD 2>/dev/null)
    # Handle detached HEAD state
    if [ "$branch" = "HEAD" ]; then
        local commit_hash=$(git rev-parse --short HEAD 2>/dev/null)
        branch="($commit_hash)"
    fi

    local dirty_color=""
    if [[ -z "$(git status --porcelain --ignore-submodules -unormal)" ]]; then
        # No changes
        dirty_color="%F{green}"
    else
        # Changes present
        dirty_color="%F{red}"
    fi

    echo " ${dirty_color}${branch}%{%f%}"
}

venv_name() {
    if [[ -n "$VIRTUAL_ENV" ]]; then
        # Add a leading space before the virtual env name
        echo " ($(basename "$VIRTUAL_ENV"))"
    else
        # Return empty string if no virtual environment is active
        echo ""
    fi
}

short_pwd() {
    local max_width=$1
    local pwd_path=$(print -P "%~")  # Use %~ to get the path with ~ abbreviation
    local path_length=${#pwd_path}

    if [[ $max_width -gt 0 && $path_length -gt $max_width ]]; then
        local truncate_len=$((path_length - max_width + 1)) # +1 for the single ellipsis character
        local start_len=$(( (path_length - truncate_len) / 2 ))
        local end_len=$(( path_length - start_len - truncate_len ))

        echo "${pwd_path:0:$start_len}…${pwd_path: -$end_len}"
    else
        echo "$pwd_path"
    fi
}
#####


###  Prompt Function  ###
# Left-prompt is constant char+space to maximize space and eliminate start-of-line noise; all info goes in the right-prompt
# Right-prompt starts with constant char to allow relatively easy parsing/trimming of copied terminal lines
# Both prompts are color-coded based on success/failure of last command
# The full right-prompt is:
#   ● [exit code] (venv) branch ~/current/directory
set_prompt() {
    PS1="%{%F{%(?.blue.red)}%}▶︎ %{%f%}"
    RPROMPT="%{%F{%(?.blue.red)}%}●%(?.. [%?])%{%f%}$(venv_name)$(branch_name) %{%F{blue}%}$(short_pwd 30)%{%f%}"
}
PS2="%{%F{blue}%}… %{%f%}"  # Continuation
PS3="%{%F{blue}%}? %{%f%}"  # Select
PS4="%{%F{blue}%}▷ %{%f%}"  # Debug

typeset -ga precmd_functions
precmd_functions+=(set_prompt)  # Keep prompt updated
#####
